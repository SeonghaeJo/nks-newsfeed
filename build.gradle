plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

allprojects {
    group = 'com.personal'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

tasks.register('redisUp', Exec) {
    group = 'local'
    description = 'Start Redis with Docker Compose'
    workingDir = file("${rootProject.projectDir}/local/compose")
    commandLine '/opt/homebrew/bin/docker-compose', 'up', '-d'
}

tasks.register('redisDown', Exec) {
    group = 'local'
    description = 'Stop Redis with Docker Compose'
    workingDir = file("${rootProject.projectDir}/local/compose")
    commandLine '/opt/homebrew/bin/docker-compose', 'down'
}

tasks.register('redisLogs', Exec) {
    group = 'local'
    description = 'Show Redis logs'
    workingDir = file("${rootProject.projectDir}/local/compose")
    commandLine '/opt/homebrew/bin/docker-compose', 'logs', '-f', 'redis'
}

tasks.register('containerStatus', Exec) {
    group = 'local'
    description = 'Check Container status'
    workingDir = file("${rootProject.projectDir}/local/compose")
    commandLine '/opt/homebrew/bin/docker-compose', 'ps'
}
